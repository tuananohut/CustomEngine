Texture2D shaderTexture: register(t0);
Texture2D depthMapTexture: register(t1);
SamplerState SampleTypeClamp: register(s0);
SamplerState SampleTypeWrap: register(s1);

struct PixelInputType
{
	float4 position: SV_POSITION;
	float2 tex: TEXCOORD0;
	float3 normal: NORMAL;
	float4 lightViewPosition: TEXCOORD1;
};

cbuffer LightBuffer
{
	float4 ambientColor;
	float4 diffuseColor;
	float3 lightDirection;
	float bias;
};

float4 ShadowPixelShader(PixelInputType input): SV_TARGET
{
	float4 color;
	float2 projectTexCoord;
	float depthValue;
	float lightDepthValue;
	float lightIntensity;
	float4 textureColor;
	float3 lightDir;

	lightDir = -lightDirection;

	color = ambientColor; 

	projectTexCoord.x = input.lightViewPosition.x / input.lightViewPosition.w / 2.f + 0.5f;
	projectTexCoord.y = -input.lightViewPosition.y / input.lightViewPosition.w / 2.f + 0.5f;

	if ((saturate(projectTexCoord.x) == projectTexCoord.x) && (saturate(projectTexCoord.y) == projectTexCoord.y))
	{
		depthValue = depthMapTexture.Sample(SampleTypeClamp, projectTexCoord).r;

		lightDepthValue = input.lightViewPosition.z / input.lightViewPosition.w;

		lightDepthValue = lightDepthValue - bias;

		if (lightDepthValue < depthValue)
		{
			lightIntensity = saturate(dot(input.normal, lightDir));

			if(lightIntensity > 0.f)
			{
				color += (diffuseColor * lightIntensity);

				color = saturate(color);
			}
		}
	}

	else
	{
		lightIntensity = saturate(dot(input.normal, lightDir));
		
		if (lightIntensity > 0.f)
		{
			color += (diffuseColor * lightIntensity);
			color = saturate(color);
		}
	}
	
	
	textureColor = shaderTexture.Sample(SampleTypeWrap, input.tex);

	color = color * textureColor;
	
	return color;
}
